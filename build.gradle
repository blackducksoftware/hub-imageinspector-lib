buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
    		classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.69'
		classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
		classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
    }
}

plugins { id 'groovy' }

project.ext.moduleName = 'com.synopsys.integration.hub-imageinspector-lib'
project.ext.javaUseAutoModuleName = 'true'
project.ext.junitShowStandardStreams = 'true'

apply plugin: "io.spring.dependency-management"

version = '10.0.1'

dependencyManagement {
    imports {
    	mavenBom 'io.spring.platform:platform-bom:Brussels-SR7'
    }
}

apply plugin: 'com.blackducksoftware.integration.library'

dependencies {
    api 'com.blackducksoftware.integration:integration-bdio:17.1.2'
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework:spring-context'
    api 'commons-io:commons-io:2.5'
    api 'org.apache.commons:commons-lang3:3.7'
    api 'org.apache.commons:commons-compress:1.15'
    api 'org.apache.commons:commons-exec:1.3'

    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

repositories {
	mavenCentral()
}

////////////////////////////////////////////////
// For integration tests: Build image tarfiles

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile 

task removeTestImage(type: Exec) {
    ignoreExitValue true
    commandLine "docker", "rmi", "blackducksoftware/centos_minus_vim_plus_bacula:1.0"
}

task removeTestBaseImage(type: Exec) {
    ignoreExitValue true
    commandLine "docker", "rmi", "centos:7.3.1611"
}

task createTestDockerfile(type: Dockerfile) {
	destFile = project.file("${buildDir}/images/test/centos_minus_vim_plus_bacula/Dockerfile")
	println "destFile: ${destFile}"
	from 'centos:7.3.1611'
	maintainer 'Black Duck Software'
	environmentVariable('LANG', 'en_US.UTF-8')
	
	runCommand 'rpm -e vim-minimal && \
        yum install -y bacula-director-5.2.13-23.1.el7 bacula-storage-5.2.13-23.1.el7 bacula-client-5.2.13-23.1.el7 \
        bacula-console-5.2.13-23.1.el7'
}

task buildTestDockerImage(type: Exec, dependsOn: [removeTestBaseImage, removeTestImage, createTestDockerfile]) {
	commandLine "docker", "build", "--no-cache", "--tag", "blackducksoftware/centos_minus_vim_plus_bacula:1.0", \
        "${buildDir}/images/test/centos_minus_vim_plus_bacula"
}

task buildTestDockerTarfile(type: Exec, dependsOn: buildTestDockerImage) {
    outputs.files file("${buildDir}/images/test/centos_minus_vim_plus_bacula.tar")
	commandLine "docker", "save", "-o", "${buildDir}/images/test/centos_minus_vim_plus_bacula.tar", \
        "blackducksoftware/centos_minus_vim_plus_bacula:1.0"
}

task pullAlpineLatest(type: Exec) {
    commandLine "docker", "pull", "alpine:latest"
}

task createImagesDir(type: Exec) {
	commandLine "mkdir", "-p", "${buildDir}/images/test"
}

task buildAlpineTestDockerTarfile(type: Exec, dependsOn: [createImagesDir, pullAlpineLatest]) {
    commandLine "docker", "save", "-o", "${buildDir}/images/test/alpine.tar", "alpine:latest"
}

task createAlpineDir(type: Exec) {
    commandLine "mkdir", "-p", "build/images/test/alpine/tarExtraction/alpine.tar"
}

task buildTestTarfiles(dependsOn: [buildTestDockerTarfile, buildAlpineTestDockerTarfile]) {}

task unTarAlpine(type: Exec, dependsOn: [buildTestTarfiles, createAlpineDir]) {
    workingDir "build/images/test/alpine/tarExtraction/alpine.tar"
    commandLine "tar", "xvf", "../../../alpine.tar"
}

testIntegration.dependsOn unTarAlpine

// End of stuff for integration tests
////////////////////////////////////////////////
