buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
		classpath "com.blackducksoftware.integration:common-gradle-plugin:0.0.+"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
		classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
    }
}

plugins {
//    id "io.spring.dependency-management" version "1.0.0.RELEASE"
    id 'org.sonarqube' version '2.6.1'
}
apply plugin: "io.spring.dependency-management"

version = '1.1.1-SNAPSHOT'
apply plugin: 'com.blackducksoftware.integration.library'

dependencyManagement {
    imports {
    	mavenBom 'io.spring.platform:platform-bom:Brussels-SR7'
    }
}

dependencies {
    compile 'com.blackducksoftware.integration:integration-bdio:12.1.0'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework:spring-context'
    compile 'commons-io:commons-io:2.5'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.apache.commons:commons-compress:1.15'
    compile 'org.apache.commons:commons-exec:1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

repositories {
	mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Integrations - Hub Image Inspector Library'
        property 'sonar.projectKey', 'com.blackducksoftware:int:hub-imageinspector-lib'
        property 'sonar.verbose', false
   }
}
////////////////////////////////////////////////
// For integration tests: Build image tarfiles



apply plugin: 'com.bmuschko.docker-remote-api'
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

task removeTestImage(type: Exec) {
    ignoreExitValue true
    commandLine "docker", "rmi", "blackducksoftware/centos_minus_vim_plus_bacula:1.0"
}

task removeTestBaseImage(type: Exec) {
    ignoreExitValue true
    commandLine "docker", "rmi", "centos:7.3.1611"
}

task createTestDockerfile(type: Dockerfile) {
	destFile = project.file("${buildDir}/images/test/centos_minus_vim_plus_bacula/Dockerfile")
	println "destFile: ${destFile}"
	from 'centos:7.3.1611'
	maintainer 'Black Duck Software'
	environmentVariable('LANG', 'en_US.UTF-8')
	
	runCommand 'rpm -e vim-minimal && \
        yum install -y bacula-director-5.2.13-23.1.el7 bacula-storage-5.2.13-23.1.el7 bacula-client-5.2.13-23.1.el7 \
        bacula-console-5.2.13-23.1.el7'
}

task buildTestDockerImage(type: Exec, dependsOn: [removeTestBaseImage, removeTestImage, createTestDockerfile]) {
	commandLine "docker", "build", "--no-cache", "--tag", "blackducksoftware/centos_minus_vim_plus_bacula:1.0", \
        "${buildDir}/images/test/centos_minus_vim_plus_bacula"
}

task buildTestDockerTarfile(type: Exec, dependsOn: buildTestDockerImage) {
    outputs.files file("${buildDir}/images/test/centos_minus_vim_plus_bacula.tar")
	commandLine "docker", "save", "-o", "${buildDir}/images/test/centos_minus_vim_plus_bacula.tar", \
        "blackducksoftware/centos_minus_vim_plus_bacula:1.0"
}

task pullAlpineLatest(type: Exec) {
    commandLine "docker", "pull", "alpine:latest"
}

task createImagesDir(type: Exec) {
	commandLine "mkdir", "-p", "${buildDir}/images/test"
}

task buildAlpineTestDockerTarfile(type: Exec, dependsOn: [createImagesDir, pullAlpineLatest]) {
    commandLine "docker", "save", "-o", "${buildDir}/images/test/alpine.tar", "alpine:latest"
}

task buildTestTarfiles(dependsOn: [buildTestDockerTarfile, buildAlpineTestDockerTarfile]) {}

testIntegration.dependsOn buildTestTarfiles

// End of stuff for integration tests
////////////////////////////////////////////////
